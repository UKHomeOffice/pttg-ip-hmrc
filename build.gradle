buildscript {

  ext {
    springBootVersion = '2.0.5.RELEASE'
  }

  repositories {
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath 'net.researchgate:gradle-release:2.6.0'
  }
}

version = '0.0.1'

if (System.getenv('VERSION')) {
  version = System.getenv('VERSION')
}
if (System.getenv('BUILD_NUMBER')) {
  version = version + '-' + System.getenv('BUILD_NUMBER')
}


apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'project-report'
apply plugin: 'net.researchgate.release'

checkstyleTest.enabled = false
checkstyle {
  toolVersion = "5.9"
  ignoreFailures = false
}

findbugs {
  ignoreFailures = false
}

tasks.withType(FindBugs) {
  excludeFilter = file('config/findbugs/exclude-filter.xml')
  reports {
    xml.enabled false
    html.enabled true
  }
}


jar {
  baseName = 'pttg-ip-hmrc'
  if (System.getenv('GIT_COMMIT')) {
    version = version + '.' + System.getenv('GIT_COMMIT')
  }
  manifest {
    attributes 'Implementation-Title': baseName,
               'Implementation-Version': version
  }
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

configurations {
  compile.exclude module: 'spring-boot-starter-tomcat'
  compile.exclude group: 'org.apache.tomcat'
}

dependencies {

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.16.12'

  compile group: 'org.springframework.boot',  name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot',  name: 'spring-boot-starter-jetty'
  compile group: 'org.springframework.boot',  name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot',  name: 'spring-boot-starter-hateoas'
  compile group: 'org.springframework',       name: 'spring-aspects'
  compile group: 'org.springframework.retry', name: 'spring-retry'
  compile group: "org.springframework.boot",  name: "spring-boot-configuration-processor"

  compile group: 'net.logstash.logback',      name: 'logstash-logback-encoder',   version: '5.0'

  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.8.3'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
  compile group: 'com.jayway.jsonpath', name: 'json-path'
  compile group: 'com.google.guava', name: 'guava', version: '21.0'
  compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'

  compile group: 'me.xuender',      name: 'unidecode',  version: '0.0.7'

  compileOnly group: 'org.springframework.boot',  name: 'spring-boot-devtools'
  compileOnly group:  'org.projectlombok',            name: 'lombok',                     version: '1.16.12'

  testCompile (group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
    exclude group: 'com.vaadin.external.google', module: 'android-json'
  }
  testCompile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'
  testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4-rc-2'
  testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4-rc-2'

  testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.7'
  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
  testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.18.0'
  testCompile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
  testCompile group: 'cglib', name: 'cglib-nodep', version: '3.2.5'
  testCompile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: '2.9.3'

  testCompile group: 'net.serenity-bdd', name: 'serenity-cucumber', version: '1.9.18'
  testCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: '2.0.7'

  testCompile group: 'info.cukes', name:'gherkin', version: '2.12.2'

  testCompile "com.jayway.restassured:json-schema-validator:2.9.0"
  testCompile ("com.jayway.restassured:rest-assured:2.9.0") {
    exclude group: "org.codehaus.groovy", module: "groovy"
  }

  testCompile group: 'org.json', name: 'json', version: '20180813'

  testCompile 'org.projectlombok:lombok:1.16.12'

}


gradle.startParameter.continueOnFailure = false

release {
  preTagCommitMessage = '[Gradle Release Plugin] (EE-9726) - pre tag commit: '
  tagCommitMessage = '[Gradle Release Plugin] (EE-9726) - creating tag: '
  newVersionCommitMessage = '[Gradle Release Plugin] (EE-9726) - new version commit: '
}